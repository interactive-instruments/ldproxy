plugins {
    id "com.moowork.node" version "1.3.1"
}

dependencies {
    api project(":ogcapi-foundation")
}

compileJava {
    File generatedResourcesDir = new File(project.buildDir, 'generated/src/main/resources/')
    project.sourceSets.main.resources { srcDir generatedResourcesDir }
    outputs.dir(generatedResourcesDir)
}

jar {
    manifest {
        attributes('X-Web-Resource-Version': '1.1')
        attributes('X-Web-Resource': '/ogcapi-html/assets;/assets')
    }
}

node {
    version = '14.17.6'
    yarnVersion = '1.22.5'
    download = project.findProperty('downloadNode') != 'false' ?: false
}

yarn_install {
    execOverrides {
        it.workingDir = 'src/main/javascript'
    }
}

task yarn_build(type: YarnTask) {
    execOverrides {
        it.workingDir = 'src/main/javascript'
    }
    args = ['run', 'build']
}

yarn_build.dependsOn yarn_install

yarn_start {
    execOverrides {
        it.workingDir = 'src/main/javascript'
    }
}

processResources.dependsOn yarn_build
//check.dependsOn yarn_build
yarn_build.finalizedBy jar
