plugins {
    id "com.moowork.node" version "1.3.1"
}

dependencies {
    api project(":ogcapi-foundation")
}

ext {
    generatedResourcesDir = new File(project.buildDir, 'generated/src/main/resources/')
}

compileJava {
    project.sourceSets.main.resources { srcDir generatedResourcesDir }
    outputs.dir(generatedResourcesDir)
}

jar {
    manifest {
        attributes('X-Web-Resource-Version': '1.1')
        attributes('X-Web-Resource': '/ogcapi-html/assets;/assets')
    }
}

node {
    version = '14.17.6'
    yarnVersion = '1.22.5'
    download = project.findProperty('downloadNode') != 'false' ?: false
}

yarn_install {
    inputs.file("src/main/javascript/package.json")
    inputs.file("src/main/javascript/yarn.lock")
    outputs.dir("src/main/javascript/node_modules")
    outputs.dir("src/main/javascript/.yarn/cache")
    execOverrides {
        it.workingDir = 'src/main/javascript'
    }
}

task yarn_build(type: YarnTask) {
    dependsOn yarn_install
    inputs.file("src/main/javascript/package.json")
    inputs.file("src/main/javascript/yarn.lock")
    inputs.dir("src/main/javascript/src")
    outputs.dir(generatedResourcesDir)
    execOverrides {
        it.workingDir = 'src/main/javascript'
    }
    args = ['run', 'build']
}

yarn_start {
    execOverrides {
        it.workingDir = 'src/main/javascript'
    }
}

processResources.dependsOn yarn_build
