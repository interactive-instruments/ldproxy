plugins {
    alias catalog.plugins.xtraplatform.layer
    alias catalog.plugins.spotless apply false
}

allprojects {
    group = 'de.interactive_instruments'
}

apply from: '../version.gradle'

apply from: '../xtraplatform.gradle'

apply from: '../formatting.gradle'

dependencies {
    layers group: 'de.interactive_instruments', name: 'ogcapi-stable', version: version

    // modules to include in this layer
    modules subprojects
}

//TODO
layer {
    CANDIDATE {
        minimumCoverage = 0.0
    }
}

subprojects {
    dependencies {
        provided 'de.interactive_instruments:xtraplatform-entities'
        provided 'de.interactive_instruments:xtraplatform-openapi'
        provided 'de.interactive_instruments:xtraplatform-services'
        provided 'de.interactive_instruments:xtraplatform-values'
        provided 'de.interactive_instruments:ogcapi-foundation'
    }
}

task checkAll { dependsOn subprojects*.tasks*.findByPath('check') }
task testAll { dependsOn subprojects*.tasks*.findByPath('test') }
tasks.publish { dependsOn subprojects*.tasks*.findByPath('publish') }
task cleanAll { dependsOn subprojects*.tasks*.findByPath('clean') + tasks.clean }
task spotlessApplyAll { dependsOn subprojects*.tasks*.findByPath('spotlessApply') }

// TODO: workaround for issues with hivemq-client which also uses dagger;
// our dagger will recompile hivemq classes which leads to various errors;
// dagger does not touch classes in the extra module generated for intellij, 
// so we enforce that 
if (System.getProperty("idea.active") != "true") {
    configure(subprojects) {
        it.beforeEvaluate {
            System.setProperty('idea.active', it.name == 'ogcapi-pubsub' ? 'true' : 'false');
        }
    }
}
