buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath group: 'de.interactive_instruments', name: 'gradle-plugin-osgi-ipojo', version: '1.12.1.32'
        classpath "gradle.plugin.nl.javadude.gradle.plugins:license-gradle-plugin:0.14.0"
        classpath 'com.netflix.nebula:nebula-ospackage-plugin:3.+'
        classpath 'biz.aQute.bnd:biz.aQute.bnd.gradle:3.5.0'
        classpath files('buildSrc/build/libs/buildSrc-all.jar')
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
    }
}

apply plugin: 'application'

version = '1.4.0'
//version += '-SNAPSHOT'
group = 'de.interactive_instruments'

repositories {
    jcenter()
    mavenCentral()
    maven {
        url "https://dl.bintray.com/iide/maven"
    }
    mavenLocal()
}

configurations {
    bundles
    bundlesRuntime
}

dependencies {
    // TODO: are all the jars in lib dependencies of this?
    compile group: 'de.interactive_instruments', name: 'xtraplatform-runtime', version: '1.1.7'

    bundlesRuntime group: 'de.interactive_instruments', name: 'xtraplatform-bundles-runtime', version: '1.1.5'

    bundles(group: 'de.interactive_instruments', name: 'xtraplatform-bundles-core', version: '1.2.6') {
        // TODO
        exclude module: 'xtraplatform-kvstore-inmemory'
    }

    bundles group: 'de.interactive_instruments', name: 'xtraplatform-sdi-tools', version: '2.1.0'

    // activate for access to the Apache Felix console at /system/console
    //bundles group: 'de.interactive_instruments', name: 'xtraplatform-bundles-devel', version: '1.1.1'

    // ???
    subprojects.each {
        bundles(it) {
            transitive = false
        }
    }
}

// TODO env plugin
configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
}

mainClassName = "de.ii.xtraplatform.runtime.FelixRuntime"

/*task devBundles(type: Sync) {
    from configurations.bundles.resolvedConfiguration.resolvedArtifacts.findAll({
        it.moduleVersion.id in configurations.bundles.incoming.resolutionResult.root.dependencies.collect({it.selected.dependencies}).collectNested({it.selected.moduleVersion}).flatten()
    }).collect({it.file})
    into "$buildDir/bundles/runtime"
}*/

distributions {
    main {
        contents {
            from(configurations.bundles) {
                into "bundles/platform"
            }
            from(configurations.bundlesRuntime) {
                into "bundles/runtime"
            }
            into('') {
                //create an empty 'data/log' directory in distribution root
                def appDirBase = new File(buildDir, 'tmp/app-dummy-dir')
                def logDir = new File(appDirBase, 'data/log')
                logDir.mkdirs()

                from {appDirBase}
            }
        }
    }
}
distTar.version = ''

ext {
    dataDir = new File(buildDir, 'data')
}

task initData {
    doLast {
        dataDir.mkdirs()
    }
}

task cleanFelixCache(type: Delete) {
    delete new File(dataDir, 'felix-cache')
}

run {
    dependsOn installDist
    dependsOn initData
    dependsOn cleanFelixCache
    workingDir = installDist.destinationDir
    args dataDir.absolutePath
    standardInput = System.in
}

tasks.startScripts.unixStartScriptGenerator.template = resources.text.fromFile('gradle/sh-start-script')


apply from: 'bundles.gradle'

apply from: 'docker.gradle'

//apply from: '../spatialdataontheweb/packaging.gradle'

apply from: 'license-report.gradle'

//apply plugin: 'xtraplatform-docs'

apply from: 'git-hooks.gradle'

/*

gradle-plugin-xtraplatform-docs-source

xpdocs {
    source: {
        format: markdown|asciidoc,
        layout: default|arc42,
        arc42: {
            goals: docs/01_introduction_and_goals.md,
            ...
        }
    }
}


gradle-plugin-xtraplatform-docs

xpdocs {
    target: {
        processor: default|asciidoctor,
        theme: ii,
        style: {
            primaryColor: #111111,
            ...
        }
    }
}

*/
task allDependencies {
    dependsOn allprojects.collect { "$it.path:dependencies" }
}
