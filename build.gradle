buildscript {
    dependencies {
        classpath (group: 'de.interactive_instruments', name: 'xtraplatform-modules', version: '4.0.0-SNAPSHOT') {
            changing = true
        }
    }
    configurations.all {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    }
}
plugins {
    id 'de.interactive_instruments.xtraplatform-application' version '4.0.0-SNAPSHOT'
    id 'com.github.hierynomus.license-report' version '0.16.1' apply false
    id 'com.bmuschko.docker-java-application' version '3.1.0' apply false
}

allprojects {
    group = 'de.interactive_instruments'
}

apply from: 'version.gradle'

apply from: 'xtraplatform.gradle'

dependencies {
    // features to include in this application
    feature group: 'de.interactive_instruments', name: 'ogcapi-stable'
    feature group: 'de.interactive_instruments', name: 'ogcapi-draft'

    if (project.hasProperty('community')) {
        feature group: 'de.interactive_instruments', name: 'ldproxy-community', version: '1.0.0-SNAPSHOT'
    }

    // bundles to include in this application
    bundle subprojects
}

ext {
    useNativeRun = org.gradle.internal.os.OperatingSystem.current().isLinux()
            ? project.findProperty('runInDocker') != 'true'
            : project.findProperty('runInDocker') == 'false'
}

tasks.run {
    debug = project.findProperty('debug') ?: false
    args = project.findProperty('data') ? [project.findProperty('data')] : args
    onlyIf {useNativeRun}
    doFirst {
        println "useNativeRun: ${useNativeRun}, isLinux: ${org.gradle.internal.os.OperatingSystem.current().isLinux()}, runInDocker: ${project.findProperty('runInDocker')}"
        if (useNativeRun && !org.gradle.internal.os.OperatingSystem.current().isLinux()) {
            logger.warn("WARNING: Running natively is not supported for '${System.getProperty("os.name")}' and will most likely lead to errors.")
        }
    }
}

tasks.test {
    dependsOn project.gradle.includedBuild('ogcapi-stable').task(':testAll')
    dependsOn project.gradle.includedBuild('ogcapi-draft').task(':testAll')
}

tasks.check {
    dependsOn project.gradle.includedBuild('ogcapi-stable').task(':checkAll')
    dependsOn project.gradle.includedBuild('ogcapi-draft').task(':checkAll')
}

tasks.publish {
    dependsOn project.gradle.includedBuild('ogcapi-stable').task(':publish')
    dependsOn project.gradle.includedBuild('ogcapi-draft').task(':publish')
}

apply from: 'license-headers.gradle'

apply from: 'docker.gradle'

//apply from: 'license-report.gradle'

//apply plugin: 'xtraplatform-docs'

apply from: 'git-hooks.gradle'

