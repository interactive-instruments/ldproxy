plugins {
    id 'de.interactive_instruments.xtraplatform-application' version '3.5.1'
    id 'com.github.hierynomus.license-report' version '0.15.0' apply false
    id 'com.bmuschko.docker-java-application' version '3.1.0' apply false
}

allprojects {
    group = 'de.interactive_instruments'
}

apply from: 'version.gradle'

apply from: 'xtraplatform.gradle'

dependencies {
    // features to include in this application
    featureDevOnly group: 'de.interactive_instruments', name: 'xtraplatform-dev', version: '3.5.0'

    feature group: 'de.interactive_instruments', name: 'ogcapi-stable'
    feature group: 'de.interactive_instruments', name: 'ogcapi-draft'

    if (project.hasProperty('community')) {
        feature group: 'de.interactive_instruments', name: 'ldproxy-community', version: '1.0.0-SNAPSHOT'
    }

    // bundles to include in this application
    bundle subprojects
}

tasks.run {
    debug = project.findProperty('debug') ?: false
    args = project.findProperty('data') ? [project.findProperty('data')] : args 
}

tasks.test {
    dependsOn project.gradle.includedBuild('ogcapi-stable').task(':testAll')
    dependsOn project.gradle.includedBuild('ogcapi-draft').task(':testAll')
}

tasks.check {
    dependsOn project.gradle.includedBuild('ogcapi-stable').task(':checkAll')
    dependsOn project.gradle.includedBuild('ogcapi-draft').task(':checkAll')
}

tasks.publish {
    dependsOn project.gradle.includedBuild('ogcapi-stable').task(':publish')
    dependsOn project.gradle.includedBuild('ogcapi-draft').task(':publish')
}

apply from: 'immutables.gradle'

apply from: 'license-headers.gradle'

apply from: 'docker.gradle'

apply from: 'license-report.gradle'

//apply plugin: 'xtraplatform-docs'

apply from: 'git-hooks.gradle'

