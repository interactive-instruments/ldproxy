settings.pluginManagement {
    repositories {
        maven {
            url "https://dl.interactive-instruments.de/repository/maven-releases/"
        }
        maven {
            url "https://dl.interactive-instruments.de/repository/maven-snapshots/"
        }
        gradlePluginPortal()
    }
}

apply from: '../catalog.gradle'

rootProject.name = 'ldproxy-composite'

def siblingDirectories = rootDir.parentFile.parentFile.listFiles()
def xtraplatform = siblingDirectories.find({ it.name.toLowerCase().equals('xtraplatform') })
def xtraplatformSpatial = siblingDirectories.find({ it.name.toLowerCase().equals('xtraplatform-spatial') })
def xtraplatformBuild = siblingDirectories.find({ it.name.toLowerCase().equals('xtraplatform-build') })
def xtraplatformNative = siblingDirectories.find({ it.name.toLowerCase().equals('xtraplatform-native') })

if (hasProperty('build-plugins') && xtraplatformBuild) {
    includeBuild "../../${xtraplatformBuild.name}"
}
if (xtraplatform) {
    includeBuild "../../${xtraplatform.name}/xtraplatform-core"
}
if (hasProperty('native') && xtraplatformNative) {
    includeBuild "../../${xtraplatformNative.name}"
}
if (xtraplatformSpatial) {
    includeBuild "../../${xtraplatformSpatial.name}"
}

includeBuild '../ogcapi-stable'
includeBuild '../ogcapi-draft'
includeBuild '../ogcapi-custom'

includeBuild '../'
